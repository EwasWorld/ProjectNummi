plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

ext.minimumSdkVersion = 21 // 2 digits
ext.versionMajor = 0 // 2 digits
ext.versionMinor = 2 // 2 digits
ext.versionPatch = 0 // 1 digit
ext.extraInfo = 0 // 2 digits (currently unused)
ext.versionClassifier = null // Extra info to be appended to version name e.g. alpha (used for version name)
ext.isSnapshot = false // Will append SNAPSHOT to version name

@SuppressWarnings('GrMethodMayBeStatic')
private Integer generateVersionCode() {
    // There is one digit spare between min sdk and major version
    return ext.minimumSdkVersion * 10000000 + ext.extraInfo * 100000 + ext.versionMajor * 1000 + ext.versionMinor * 10 + ext.versionPatch
}

@SuppressWarnings('GrMethodMayBeStatic')
private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}

android {
    namespace 'com.eywa.projectnummi'
    compileSdk 32

    defaultConfig {
        applicationId "com.eywa.projectnummi"
        minSdk project.ext.minimumSdkVersion
        targetSdk 32
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.1.1'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    productFlavors {
        flavorDimensions("environment")
        create("local") {
            dimension = "environment"
        }
        create("live") {
            dimension = "environment"
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (variant.buildType.name == 'release' && !names.contains("live")) {
            setIgnore(true)
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.3.1'
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation 'androidx.compose.material:material:1.1.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"

    def nav_version = "2.5.2"
    implementation("androidx.navigation:navigation-compose:$nav_version")

    def room_version = "2.4.3"
    implementation("androidx.room:room-runtime:$room_version")
    annotationProcessor("androidx.room:room-compiler:$room_version")
    kapt("androidx.room:room-compiler:$room_version")
    implementation("androidx.room:room-ktx:$room_version")

    implementation "androidx.datastore:datastore-preferences:1.0.0"

    implementation("com.google.dagger:hilt-android:2.44")
    implementation("androidx.hilt:hilt-navigation-compose:1.0.0")
    kapt("com.google.dagger:hilt-android-compiler:2.44")

    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1"
}

// Hilt: allow references to generated code
kapt {
    correctErrorTypes = true
}
